Hello Bokken enthusiasts!

hereby some instruction on howto build the Windows installer for Bokken:

1. Install Inno Setup
2. Open bokken-installer-win-v10.iss
3. Hit CTRL+F9 to start making the setup


More details about the installer's code is commented inside the installation script:

** Bokken Win32 installation script for Inno Setup

[Setup]
AppName=Bokken Win32
AppVersion=1.8
DefaultDirName={pf}\Bokken
DefaultGroupName=Bokken
OutputDir=bokken-installer-win-output\
OutputBaseFilename=bokken-1.8-win32
Compression=lzma2
SolidCompression=yes
LicenseFile=bokken-installer-win\license.txt
;UninstallDisplayIcon={app}\uninstall.ico
WizardImageFile=bokken-installer-win-logo-v2.bmp
WizardSmallImageFile=bokken-installer-win-minilogo.bmp

[Files]
;NOTE: the MSI files are called AfterInstall NOT BeforeInstall due the fact we need the files in TMP before we can call MSIEXEC for them
;NOTE2: the MSI files are REMOVED after installation to keep it all nice and clean, the "deleteafterinstall" flag is responsible for that
Source: "python-2.7.10.msi"; DestDir: "{tmp}"; AfterInstall: PreInstall01; Flags: deleteafterinstall
Source: "pygtk-all-in-one-2.24.2.win32-py2.7.msi"; DestDir: "{tmp}"; AfterInstall: PreInstall02; Flags: deleteafterinstall
Source: "bokken-installer-win\bokken-1.8\*.*"; DestDir: "{app}"; Flags: recursesubdirs
Source: "bokken-installer-win\Readme.txt"; DestDir: "{app}"; Flags: isreadme
Source: "bokken-installer-win\site-packages\*.*"; DestDir: "C:\Python27\Lib\site-packages"; Flags: recursesubdirs

[Run]
;NOTE: depreciated MSIEXEC deployment, left here for future purposes
;Filename: "msiexec.exe"; Parameters: "/i ""{tmp}\python-2.7.10.msi"""
;Filename: "msiexec.exe"; Parameters: "/i ""{tmp}\pygtk-all-in-one-2.24.2.win32-py2.7.msi"""
Filename: "{app}\bokken.bat"; WorkingDir: "{app}"; Description: "Launch application"; Flags: postinstall nowait skipifsilent unchecked

[Registry]
;NOTE: A reboot is required in order to set these new environmental changes, the path is currently set in the bokken.bat file so it works immediately.
;Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{olddata};{app}\graphviz-2.38\bin"; Check: NeedsAddPath('{app}\graphviz-2.38\bin')
;Root: HKLM; Subkey: "SYSTEM\CurrentControlSet\Control\Session Manager\Environment"; ValueType: expandsz; ValueName: "Path"; ValueData: "{olddata};{app}\radare2-w32-0.9.9\bin"; Check: NeedsAddPath('{app}\radare2-w32-0.9.9\bin')

[Icons]
;Programs folder
Name: "{group}\Bokken"; IconFilename: "{app}\bokken.ico"; Filename: "{app}\bokken.bat"; WorkingDir: "{app}" 
Name: "{group}\ReadMe"; Filename: "{app}\README.txt"; WorkingDir: "{app}"
Name: "{group}\Changelog"; Filename: "{app}\CHANGELOG.txt"; WorkingDir: "{app}"
Name: "{group}\License"; Filename: "{app}\LICENSE.txt"; WorkingDir: "{app}"
Name: "{group}\Uninstall Bokken"; Filename: "{uninstallexe}"
;Desktop shortcut
Name: "{commondesktop}\Bokken"; IconFilename: "{app}\bokken.ico"; Filename: "{app}\bokken.bat"; WorkingDir: "{app}"

[Code]
// Pre-Installation procedures go here, simple message box for instructions or information about upcoming procedures of the installer
procedure PreInstall01();

var
  MsgResult: Integer;
  ErrorCode: Integer;

begin
  MsgResult := SuppressibleMsgBox('Bokken requires Python 2.7 to be installed. Install Python27 now?', mbConfirmation, MB_YESNO, IDYES);
  if MsgResult = IDYES then
  begin
  ShellExec('', 'msiexec', ExpandConstant('/I "{tmp}\python-2.7.10.msi"'), '', SW_SHOWNORMAL, ewWaitUntilTerminated, ErrorCode);
  end
  else
  if MsgResult = IDNO then
  begin
  end
end;


procedure PreInstall02();

var
  MsgResult: Integer;
  ErrorCode: Integer;

begin
  MsgResult := SuppressibleMsgBox('About to install PyGTK.', mbConfirmation, MB_YESNO, IDYES);
  if MsgResult = IDYES then
  begin
  ShellExec('', 'msiexec', ExpandConstant('/I "{tmp}\pygtk-all-in-one-2.24.2.win32-py2.7.msi" INSTALLLEVEL=2'), '', SW_SHOWNORMAL, ewWaitUntilTerminated, ErrorCode);
  end
  else
  if MsgResult = IDNO then
  begin
  end
end;

function NeedsAddPath(Param: string): boolean;
var
  OrigPath: string;
begin
  if not RegQueryStringValue(HKEY_LOCAL_MACHINE,
    'SYSTEM\CurrentControlSet\Control\Session Manager\Environment',
    'Path', OrigPath)
  then begin
    Result := True;
    exit;
  end;
  // look for the path with leading and trailing semicolon
  // Pos() returns 0 if not found
  Result := Pos(';' + Param + ';', ';' + OrigPath + ';') = 0;
end;


** Required files **

{scriptroot} python-2.7.10.msi
{scriptroot} pygtk-all-in-one-2.24.2.win32-py2.7.msi
{scriptroot} bokken-installer-win-minilogo.bmp ; shows up in the right top corner of the installer
{scriptroot} bokken-installer-win-logo-v2.bmp ; the main image of the installer
{scriptroot} bokken-installer-win-v10.iss ; the inno setup script
{scriptroot} bokken-installer-win\site-packages ; additional python packages
{scriptroot} bokken-installer-win\bokken-1.8 ; bokken itself, note that there were a few fixes applied to these files in order to make it work under Windows


** A few more things to notice **

* Bokken's environment is effective immediately due the fact that the registry is NOT changed for the required paths, a .BAT file, bokken.bat, is responsible for that.
* Python 2.7 must be installed in C:\Python27, which is the default path and should not be too problematic, bokken.bat checks if c:\python27\python.exe exists
* Before Bokken ran, I've made a few code changes based off https://bitbucket.org/inguma/bokken/pull-requests/1/fixes-to-run-bokken-on-windows/diff

	Fix "TypeError: list indices must be integers, not str" on Windows
	Fix error during thumbnail generation on windows
	bokken.bat: explicit path to the python executable, pass all the arguments to bokken.py

lib/common.py:
removed: ansi_code = map(lambda x:'', ansi_code)
replaced: return string

ui/graph.py:
removed: cmd = "/usr/bin/dot -Tpng " + tmp_dot.name + " > " + tmp_dot.name + ".png" 
replaced: cmd = "dot -Tpng " + tmp_dot.name + " > " + tmp_dot.name + ".png"

The bokken.bat file has been changed as well but different than the fixes-to-run-bokken-on-windows suggest:
@echo off
title Bokken Console Window
if not exist C:\Python27\python.exe color 0c && echo ERROR^: Python.exe is NOT found^, please re-install. && PAUSE && EXIT
set path=%path%;C:\Python27\Lib\site-packages\graphviz-2.38\bin;C:\Python27\site-packages\radare2-w32-0.9.9\bin;C:\Python27\scripts;C:\Python27
python.exe bokken.py %*

Please note that if requested, a batchfile can be build that checks the location of Python so this batch is able to load dynamically.

If there's anything you would like to know more you can contact the developers through the bokken mailinglist.
